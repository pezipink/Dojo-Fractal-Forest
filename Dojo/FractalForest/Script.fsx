open System
open System.Drawing
open System.Windows.Forms

// Create a form to display the graphics
let width, height = 500, 500         
let form = new Form(Width = width, Height = height)
let box = new PictureBox(BackColor = Color.White, Dock = DockStyle.Fill)
let image = new Bitmap(width, height)
let graphics = Graphics.FromImage image
//The following line produces higher quality images, 
//at the expense of speed. Uncomment it if you want
//more beautiful images, even if it's slower.
//Thanks to https://twitter.com/AlexKozhemiakin for the tip!
//graphics.SmoothingMode <- System.Drawing.Drawing2D.SmoothingMode.HighQuality

let background = Color.White
let foreground = Brushes.Black

box.Image <- image
form.Controls.Add(box) 

// Compute the endpoint of a line
// starting at x, y, going at a certain angle
// for a certain length. 
let endpoint x y angle length =
    x + length * cos angle,
    y + length * sin angle

let flip x = (float)height - x

// Utility function: draw a line of given width, 
// starting from x, y
// going at a certain angle, for a certain length.
let drawLine (target : Graphics) (brush : Brush) 
             (x : float) (y : float) 
             (angle : float) (length : float) (width : float) =
    let x_end, y_end = endpoint x y angle length
    let origin = new PointF((single)x, (single)(y |> flip))
    let destination = new PointF((single)x_end, (single)(y_end |> flip))
    let pen = new Pen(brush, (single)width)
    target.DrawLine(pen, origin, destination)

let draw x y angle length width = 
    drawLine graphics foreground x y angle length width

let pi = Math.PI

// Now... your turn to draw
// The trunk
draw 250. 50. (pi*(0.5)) 100.0 4.
let x, y = endpoint 250. 50. (pi*(0.5)) 100.
// first and second branches
draw x y (pi*(0.5 + 0.3)) 50. 2.
draw x y (pi*(0.5 - 0.4)) 50. 2.

// once the form is displayed, you are still able to use it
// via the FSI session. You just need to call form.Refresh()
// in order to display any pending graphics
form.Show()

// here is a handy function to clear the form
let clear() = 
    graphics.Clear background
    form.Refresh()




(* To do a nice fractal tree, using recursion is
probably a good idea. The following link might
come in handy if you have never used recursion in F#:
http://en.wikibooks.org/wiki/F_Sharp_Programming/Recursion
*)